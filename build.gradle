plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://repository.jboss.org/nexus/content/groups/public' }
}

configurations {
    codegen
}

dependencies {
    codegen group: 'org.hibernate', name: 'hibernate-core', version: '5.4.19.Final'
    codegen group: 'org.hibernate', name: 'hibernate-tools', version: '5.4.19.Final'
    codegen group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5'
    codegen group: 'log4j', name: 'log4j', version: '1.2.17'
    codegen group: 'com.oracle.database.jdbc', name: 'ojdbc10', version: '19.7.0.0'
    codegen group: 'com.h2database', name: 'h2', version: '1.4.193'
}

project.ext {
    hibernateDestDir = file("$buildDir/generated")
}

ant.lifecycleLogLevel = "VERBOSE"
task codegen {
    outputs.dir hibernateDestDir

    doLast {
        hibernateDestDir.exists() || hibernateDestDir.mkdirs()
        ant {
            echo("$buildDir/generated")
            taskdef(name: "hibernatetool",
                    classname: "org.hibernate.tool.ant.HibernateToolTask",
                    classpath: configurations.codegen.asPath + ":$projectDir/src/main/resources")
            hibernatetool(destdir: hibernateDestDir) {
                classpath {
                    pathelement(path: configurations.codegen.asPath)
                }
                configuration {
                    propertyfile="$projectDir/src/main/resources/hibernate.properties"
                        fileset(dir: "$projectDir/src") {
                            include { name = "**/*.hbm.xml" }
                    }
                }
                hbm2ddl()
                hbm2dao(destdir: "$buildDir/generated")
                hbm2java(destdir: "$buildDir/generated")
            }
        }
    }
}
